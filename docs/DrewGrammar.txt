GRAMMAR 
# This is out of date at the moment. See AST for the up-to-date gramamr.
program      : statement *                                                     ;
statement    : printstat           
             | blockstat   
             | ifstat      
             | whilestat   
             | assignstat
             | funcdef
             | funccall
             | exprstat
             ;

assignstat   : NAME '='  expr ';'                                               ;  
printstat    : 'print' '(' expr ')' ';'                                         ;
ifstat       : 'if' '(' test ')' statement                                      ;
whilestat    : 'while' '(' test ')' statement                                   ;
blockstat    : '{' statement *  '}'                                             ;
exprstat     : expr ';'                                                         ;

expr         : term ( add_op term ) *                                           ;
term         : atom ( mult_op atom ) *                                          ;
atom         : NAME | NUMBER | STRING | funccall                                ;
test         : expr cmp_op expr                                                 ;

funcdef      : 'def' NAME '(' namelist ? ')' blockstat                          ;
funccall     : NAME '(' exprlist ? ')' ';'                                      ;
exprlist     : expr ( ',' expr ) *                                              ;
namelist     : NAME ( ',' NAME ) *                                              ;

cmp_op       : '==' | '>=' | '<=' | '>' | '<'                                   ;
add_op       : '+' | '-'                                                        ;
mult_op      : '*' | '/'                                                        ;

END

# ---------------------------------------------------------------------------

AST

program         : ^ $PROGRAM statement *    ; 
statement       :  ^ ( printstat 
                |  blockstat 
                |  ifstat 
                |  whilestat 
                |  assignstat 
                |  funcdef 
                |  funccall 
                |  dowhile
                |  classdef 
                |  exprstat )
                ;

assignstat      : NAME '=' expr ';'                     -> ^( '=' NAME expr )               ; 
blockstat       : $BLOCKSTAT '{' statement *  '}'       -> ^( $BLOCKSTAT statement )        ; 
printstat       : 'print' '(' expr ')' ';'              -> ^( 'print' expr )                ;
ifstat          : 'if' '(' test ')' statement           -> ^( 'if' test statement )         ; 
whilestat       : 'while' '(' test ')' statement        -> ^( 'while' test statement )      ;
dowhile         : 'do' blockstat 'while' '(' test ')'   -> ^( 'do' test blockstat )         ;
exprstat        : expr ';'                              -> ^( expr )                        ; 
classdef        : ^ 'class' NAME blockstat                                                  ;

expr            : term  ( ^ add_op term ) *                                 ;
term            : atom  ( ^ mult_op atom ) *                                ; 
atom            : ^ ( NAME |  NUMBER |  STRING |  funccall | dotexpr )      ;
test            : expr ^ cmp_op expr                                        ;

dotexpr         : NAME ^ '.' ( NAME | funccall )                            ;

funcdef         : $FUNCDEF 'def' NAME '(' namelist ? ')' blockstat -> ^( $FUNCDEF NAME namelist blockstat )  ;
funccall        : NAME '(' exprlist ? ')' ';'             -> ^( NAME exprlist )             ;
exprlist        : $EXPRLIST expr ( ',' expr ) *           -> ^( $EXPRLIST expr expr )       ; 
namelist        : $NAMELIST NAME ( ',' NAME ) *           -> ^( $NAMELIST NAME NAME )       ;

cmp_op          : ^ ( '==' |  '>=' |  '<=' |  '>' |  '<' )      ;
add_op          : ^ ( '+' | '-' )                               ;
mult_op         : ^ ( '*' | '/' )                               ; 

END

# Map rule_name to the token_name that predicts said rule:
# I auto-generate most predicates, it would be nice to have that for ALL predicates.
# These are still hand-written beacuse my code can't yet calculate when rules will have 
# conflicting look-ahead sets, i.e. two rules are predicted by the same start token.
# I will improve my code at a later date to take care of this.

# Also, I have tokens in here? Do I need those anymore ? 


# -----------------------------------------------------------------

PREDICATES 

#namelist -> NAME
assignstat -> NAME&EQUALS
NAME -> NAME
NUMBER -> NUMBER
STRING -> STRING
GE -> GT&EQUALS
LE -> LT&EQUALS
funccall -> NAME&LPAREN
#parameters -> LPAREN
END

TOKENS: 
# Name of token:      Token Text:
#---------------------------------
COMMA                   ','
PERIOD                  '.'
LPAREN                  '('
RPAREN                  ')'
LCURBRACK               '{'
RCURBRACK               '}'  
LBRACK                  '['
RBRACK                  ']'
SEMICOLON               ';'
COLON                   ':'
EQUALS                  '='
DEQUALS                 '=='
GT                      '>'
GE                      '>='
LT                      '<' 
LE                      '<='  
QUOTE                   "'"
DQUOTE                  '"'
STAR                    '*'
PLUS                    '+'
DASH                    '-'
FSLASH                  '/'
BSLASH                  '\'
IF                      'if' 
DO                      'do' 
WHILE                   'while'
PRINT                   'print'
DEF                     'def'
CLASS                   'class'
NAME                    NON_PRE_DEF     ( ( 'a' .. 'z' ) | ( 'A' .. 'Z' ) ) *
NUMBER                  NON_PRE_DEF     ( '0' .. '9' ) * 
STRING                  NON_PRE_DEF     '"' . * '"'              
END 
                                        